apply plugin: "java"
apply plugin: 'application'
apply plugin: "jtransc"
apply plugin: 'maven'
apply plugin: 'signing'

//mainClassName = "com.jtransc.dynarec.example.DynarecExample"
mainClassName = "com.jtransc.dynarec.example.BrainfuckDynarec"

group = "com.jtransc"
version = "${jtranscVersion}"

sourceSets {
    main.java.srcDirs = [ 'src' ]
    main.resources.srcDirs = [ 'resources' ]
    test.java.srcDirs = [ 'test' ]
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "com.jtransc:jtransc-gradle-plugin:${jtranscVersion}"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "com.jtransc:jtransc-rt-core:${jtranscVersion}"
    compile 'org.ow2.asm:asm-all:5.1'
    testCompile 'junit:junit:4.12'
}


//////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

jtransc {
    minimizeNames = true
    treeshaking = true
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name "${project.name}"
                packaging 'jar'
                description 'JVM AOT compiler currently generating Javascript and Haxe, with initial focus on Kotlin and games.'
                url 'https://github.com/jtransc/jtransc-media/'

                scm {
                    url 'scm:git@github.com:jtransc/jtransc-media.git'
                    connection 'scm:git@github.com:jtransc/jtransc-media.git'
                    developerConnection 'scm:git@github.com:jtransc/jtransc-media.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'soywiz'
                        name 'Carlos Ballesteros Velasco'
                    }
                }
            }
        }
    }
}
